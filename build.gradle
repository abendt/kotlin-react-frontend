
buildscript {
    ext.kotlin_version = '1.2.21'
    ext.react_version = '16.2.0'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.27"
    }
}

group = 'org.jetbrains.kotlin.example'
version = '0.0.1-SNAPSHOT'

apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'kotlin-dce-js'

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlin-js-wrappers/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version" // for now only compile configuration is supported
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version" // for now only compile configuration is supported
}

kotlinFrontend {
    downloadNodeJsVersion = "8.9.3"

    npm {
        // karma test runner
        devDependency("karma")

        // karma plugins
        devDependency("karma-es6-shim")
        devDependency("karma-mocha-reporter")

        devDependency("karma-chrome-launcher")
    }

    karma {
        port = 9876
        runnerPort = 9100
        browsers = ["ChromeHeadless"]
        plugins = ["karma-mocha-reporter", "karma-es6-shim", "karma-chrome-launcher"]
        reporters = ["mocha"]
        frameworks = ["jasmine", "es6-shim"] // for now only qunit works as intended
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file('src/main/web')
        port = 3000
    }

    define "PRODUCTION", true

//    allBundles {
//        /* set properties for all bundles */
//    }
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

compileTestKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js-tests/${project.name}-tests.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

sourceSets {
    main {
        resources.srcDirs += "src/main/resources"
    }
}

project.afterEvaluate{
    // tasks.getByPath('webpack-config').inputs.file("$buildFile")

    tasks.matching { task -> task.name == "karma-run-single" }.all {
        outputs.upToDateWhen { false }
    }
}




